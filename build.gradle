buildscript {
    ext {
        versions = [
            kotlin           : "1.4.0",
            kotlin_coroutines: "1.3.9",
            spring           : "5.2.9.RELEASE",
            javafx           : "11.0.2"
        ]
    }
}

// This installs dependencyUpdates plugin
// Run gradle dependencyUpdates to get the report

// Path to launch4j binary
ext.launch4j = '/opt/launch4j/launch4j'
// Directories where we build the distro
ext.semver = '2.99'
ext.distBinDir = file('ganttproject-builder/dist-bin')
ext.pluginsDir = file("ganttproject-builder/dist-bin/plugins/base")
ext.buildNum = getBuildNum()

version = "${semver}-r${getBuildNum()}"


def getBuildNum() {
    return "2910"
/*
  def stdout = new ByteArrayOutputStream()
    exec {
        workingDir "ganttproject-builder"
        commandLine "bash", "-c", " git rev-list --count ganttproject-2.7.. || echo 0"
        standardOutput = stdout
    }
    return String.valueOf(Integer.valueOf("${stdout}".trim()) + 1891)
*/
}

// Config for all projects: deps come from Maven repository,
// compile using Java 8, libs normally sit in lib
allprojects {

    configurations.all {

        resolutionStrategy {

            preferProjectModules()

            force "org.jetbrains:annotations:19.0.0"
            force "com.google.guava:guava:29.0-jre"
            force "org.testcontainers:testcontainers:1.14.2"
            force "org.springframework:spring-context:5.2.9.RELEASE"
            force "org.springframework:spring-core:5.2.9.RELEASE"
            force "org.slf4j:slf4j-api:1.7.25"
            force "commons-codec:commons-codec:1.15"
            force "commons-lang:commons-lang:2.6"
            force "org.apache.poi:poi:4.1.2"
            force "org.openjfx:javafx-base:${versions.javafx}"
            force "org.openjfx:javafx-graphics:${versions.javafx}"
            force "org.openjfx:javafx-controls:${versions.javafx}"

            failOnVersionConflict()
        }
    }
    configurations {
        all*.exclude module: "commons-logging"
    }


    configurations {
        direct
        providedCompile
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "http://sandec.bintray.com/repo"
        }

    }
    apply plugin: 'java'
    ext {
        libDir = 'lib'
        mvnDir = 'lib/mvn'
    }
    sourceCompatibility = 11
    targetCompatibility = 11

    clean {
        delete += "dist-bin"
    }
}

subprojects {
    group 'biz.ganttproject'
    version = new Date().format("yy.MM.dd")
}


def addPublishing(project) {
    project.publishing {
        repositories {
            maven {
                name "ganttproject-maven-repository-internal"
                url "gcs://ganttproject-maven-repository/internal"
            }
        }
    }
}
//
//runtime {
//    distDir = rootProject.distBinDir
//    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
//    modules = ['java.base']
//}
